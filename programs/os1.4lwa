JP [const :start]
import print.4lwa
import strings.4lwa
import base10.4lwa

label start

loop {
    call printNewline
    call printTerminated [const :entercmd_str]
    call readLineTerm [const :cmd_buffer] [const 10]

    call strEq [const :cmd_buffer] [const :poke_str]
    JZ [stack neg V] [const :run_poke]

    call strEq [const :cmd_buffer] [const :peek_str]
    JZ [stack neg V] [const :run_peek]

    call strEq [const :cmd_buffer] [const :base27_str]
    JZ [stack neg V] [const :run_base27]

    call strEq [const :cmd_buffer] [const :base10_str]
    JZ [stack neg V] [const :run_base10]

    call strEq [const :cmd_buffer] [const :coredump_str]
    JZ [stack neg V] [const :run_coredump]

    JP [const :badcmd]
    
    label run_poke
    call poke_cmd
    JP [const :@continue]

    label run_peek
    call peek_cmd
    JP [const :@continue]

    label run_base27
    call base27_cmd
    JP [const :@continue]

    label run_base10
    call base10_cmd
    JP [const :@continue]

    label run_coredump
    call coredump_cmd
    JP [const :@continue]
    
    label badcmd
    call printTerminated [const :badcmd_str]
    call printNewline
}

function poke_cmd preserving A B {
    call readWord to [reg A]
    call printNewline
    call readWord to [reg B]
    MV [reg B] [reg mem A] 
    
}

function peek_cmd {
    call readWord
    call printNewline
    MV [stack mem V] [stack S]
    call printWord [stack S]
}

function base27_cmd {
    call readBase10Line
    call printWord [stack V]
}

function base10_cmd {
    call readWord
    call printNewline
    call printBase10 [stack V]
}

function coredump_cmd preserving A {
    call coreDump [const 0] [const A__]
}

# (start: addr) (end: addr) -- io
function coreDump preserving A B {
    MV [stack S] [reg B]
    MV [stack S] [reg A]

    loop {
        JE [reg A] [reg B] [const :@break]
        MV [reg first mem A] [io]
        MV [reg inc A] [reg A]
    }
}

term_string entercmd_str "Command: "
string cmd_buffer "XXXXXXXXXXXXXXXXXXXX"

term_string poke_str "poke"
term_string peek_str "peek"
term_string base27_str "base27"
term_string base10_str "base10"
term_string coredump_str "coredump"
term_string badcmd_str "Bad command"