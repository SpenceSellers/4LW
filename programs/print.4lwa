
function print preserving A B {
    MV [stack S] [reg B]                    # B is length
    MV [stack S] [reg A]                    # A is string addr
    AD [reg A] [reg B] [reg B]              # B is now end of string

    loop {
        JE [reg A] [reg B] [const :_print_end]
        MV [reg mem A] [io]
        AD [reg A] [const 4] [reg A]
    }
    label _print_end
}

function printTerminated preserving A B {
    preserve A B
    MV [stack S] [reg A] # String ptr

    loop {
        MV [reg mem A] [reg B]
        JZ [reg neg B] [const :_print_term_end]
        MV [reg B] [io]
        AD [reg A] [const 4] [reg A]
    }

    label _print_term_end
}

function printNewline {
    MV [const __C_] [io]
}

# -- (Char)
function readChar preserving A {
    label _readChar_wait
    MV [io] [reg A]
    JE [reg A] [const ZZZZ] [const :_readChar_wait]
    MV [reg A] [stack V]
}

# (Buffer: addr) (Buffer length: words) -- (read length)
function readLine preserving A B C D {
    MV [stack S] [reg B] # Buffer Length
    MV [stack S] [reg A] # Buffer Addr

    MV [const 0] [reg D] # Length of read

    loop {
        JE [reg B] [reg D] [const :_readLine_done] # We've reached length limit.

        call readChar
        MV [stack V] [reg C]

        JE [reg C] [const __C_] [const :_readLine_done]     # Is it an enter?
        MV [reg inc D] [reg D]
        MV [reg C] [reg mem A]
        AD [reg A] [const 4] [reg A] # Move buffer loc
    }

    label _readLine_done

    MV [reg D] [stack V]
}

# (Word: word) --
function printWord preserving A {
    MV [stack S] [reg A]
    MV [reg first A] [io]
    MV [reg second A] [io]
    MV [reg third A] [io]
    MV [reg fourth A] [io]
}

# Char Char Char Char -- Word
function parseWord preserving A B {
    MV [stack fourth S] [reg A]

    ML [stack fourth S] [const A_] [reg B]
    AD [reg B] [reg A] [reg A]

    ML [stack fourth S] [const A__] [reg B]
    AD [reg B] [reg A] [reg A]

    ML [stack fourth S] [const A___] [reg B]
    AD [reg B] [reg A] [reg A]

    MV [reg A] [stack V]
}

function readWord {
    call readChar
    MV [stack V] [stack S]
    call readChar
    MV [stack V] [stack S]
    call readChar
    MV [stack V] [stack S]
    call readChar
    MV [stack V] [stack S]

    FN [const :parseWord]
}
